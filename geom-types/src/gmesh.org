#+SETUPFILE: ../../src/setup.org

* Contents                                                         :toc_4_gh:
 - [[#thinggeomgmesh][thi.ng.geom.gmesh]]
     - [[#helper-functions][Helper functions]]
     - [[#constructors][Constructors]]
         - [[#creating-a-mesh-from-a-lathe][Creating a mesh from a lathe]]
         - [[#predefined-forms][Predefined forms]]
     - [[#protocol-implementations][Protocol implementations]]
         - [[#parea][PArea]]
         - [[#todo-pboundary][TODO PBoundary]]
         - [[#pbounds][PBounds]]
         - [[#pboundingsphere][PBoundingSphere]]
         - [[#todo-pcenter][TODO PCenter]]
         - [[#pflip][PFlip]]
         - [[#todo-pgraph][TODO PGraph]]
         - [[#pvertexaccess-pedgeaccess--pfaceaccess][PVertexAccess, PEdgeAccess & PFaceAccess]]
         - [[#pnormalaccess][PNormalAccess]]
         - [[#pgeomcontainer][PGeomContainer]]
         - [[#pmeshconvert][PMeshConvert]]
         - [[#ptessellate][PTessellate]]
         - [[#ptransform][PTransform]]
         - [[#pvolume][PVolume]]
         - [[#end-of-implementation][End of implementation]]
     - [[#complete-namespace-definition][Complete namespace definition]]

* thi.ng.geom.gmesh

** Helper functions

#+BEGIN_SRC clojure :noweb-ref helpers
  (defn add-face
    [{:keys [vertices edges faces] :as mesh} verts]
    (let [f (mapv #(get (find vertices %) 0 %) verts)]
      (if (and (nil? (get faces f))
               (= (count f) (count (set f))))
        (let [vertices (->> (d/wrap-seq f [(peek f)] [(first f)])
                            (d/successive-nth 3)
                            (reduce
                             (fn [acc [p c n]]
                               (d/index-kv acc c {:next n :prev p :f f}))
                             vertices))
              edges (->> (conj f (first f))
                         (d/successive-nth 2)
                         (reduce
                          (fn [acc pair] (d/index-kv acc (set pair) f))
                          edges))]
          (assoc mesh
            :vertices vertices
            :edges edges
            :faces (conj faces f)))
        mesh)))
  
  (defn vertices-planar?
    [[a b c :as verts]]
    (or (< (count verts) 4)
        (let [n (gu/ortho-normal a b c)]
          (every? #(m/delta= n (gu/ortho-normal %))
                  (d/successive-nth 3 (conj (rest verts) a))))))
  
  (defn face-neighbors-shared-edges
    [{:keys [edges]} f]
    (->> (conj f (first f))
         (d/successive-nth 2)
         (reduce
          (fn [acc pair] (into acc (-> pair set edges (disj f))))
          [])))
  
  (defn vertex-neighbors*
    [{vertices :vertices} v]
    (set/union
     (d/value-set :next vertices v)
     (d/value-set :prev vertices v)))
  
  (defn vertex-valence*
    [mesh v] (inc (count (get (:vertices mesh) v))))
  
  (defn vertex-faces*
    [mesh v] (d/value-set :f (:vertices mesh) v))
  
  (defn remove-vertex*
    [mesh v]
    (if (find (:vertices mesh) v)
      (reduce g/remove-face mesh (vertex-faces* mesh v))
      mesh))
  
  (defn replace-vertex*
    ([mesh v v2]
       (let [vfaces (vertex-faces* mesh v)]
         (-> (reduce g/remove-face mesh vfaces)
             (replace-vertex* v v2 vfaces))))
    ([mesh v v2 faces]
       (reduce #(add-face % (replace {v v2} %2)) mesh faces)))
  
  (defn merge-vertices*
    [mesh a b]
    (if ((vertex-neighbors* mesh a) b)
      (let [fa (vertex-faces* mesh a) fb (vertex-faces* mesh b)
            ab-isec (set/intersection fa fb)
            a-xor (set/difference fa ab-isec)
            b-xor (set/difference fb ab-isec)
            mp (g/mix a b)]
        (-> (reduce g/remove-face mesh (set/union ab-isec a-xor b-xor))
            (replace-vertex* a mp a-xor)
            (replace-vertex* b mp b-xor)))
      mesh))
#+END_SRC

** Constructors

#+BEGIN_SRC clojure :noweb-ref ctors
  (defn gmesh
    "Builds a new 3d mesh data structure and (optionally) populates it with
    the given items (a seq of existing meshes and/or faces). Faces are defined
    as vectors of their vertices."
    [] (thi.ng.geom.types.GMesh. {} #{} {} {} {} #{} {}))
#+END_SRC

*** Creating a mesh from a lathe

Revolving a seq of points (usually all in a plane) around an axis is a
classic and very powerful way to construct a symmetrical/cylindrical
3d mesh. The =lathe-mesh= function does exactly this (but in a more
flexible way): It takes a seq of =Vec3='s and treats them as lathe to
form a mesh. The lathe is revolved around an axis using a given
rotation fn and radial extend & resolution. The rotation fn must
accept two args: a point & rotation angle. Apart from
=rotate-around-axis= all other methods of the =PRotate3D= protocol
(implemented by =Vec3=) satisfy this condition. To use
=rotate-around-axis= as rotation fn, it needs to be wrapped in a
closure with the axis pre-configured.

The =lathe-mesh= fn first divides the given revolution angle Φ (phi)
by the given resolution and creates a seq of rotated point strips. It
then proceeds to form faces between pairs of these strips. If face
points lie on the rotation axis, triangles will be created or faces
skipped entirely to avoid degenerate meshes. Optionally, each
resulting face can also be transformed before being added to the mesh
(e.g. scaled to create gaps or subdivide). This face transform fn
should accept a seq of points and return a seq of faces (or return nil
to skip a face).

The rotation fn too can be used to not just rotate a point. E.g.
Scaling points based on Θ (the rotation angle) post-rotation can
produce very interesting results. The example function below does this for
a lathe defined in the XZ plane and centered around the Z-axis:

#+BEGIN_SRC clojure
  (fn [p theta]
    (let [s (inc (* (Math/sin (* theta 6)) 0.25))]
      (-> p
          (g/rotate-z theta) ;; first rotate
          (g/* s s 1.0))))   ;; then scale in XY plane only
#+END_SRC

Finally, in order to create a fully closed mesh, the revolution angle
Φ must be 2*Π and the first and last points of the lathe seq must be
located on the rotation axis.

#+BEGIN_SRC clojure :noweb-ref ctors
  (defn lathe-mesh
    [points res phi rot-fn & [face-fn]]
    (let [strips (mapv
                  (fn [i]
                    (let [theta (* i phi)]
                      (mapv #(let [p (rot-fn % theta)]
                               (if (m/delta= p % *eps*)
                                 % p))
                            points)))
                  (butlast (m/norm-range res)))
          strips (if (m/delta= phi m/TWO_PI)
                   (conj strips (first strips))
                   strips)
          make-face (fn [[a1 a2] [b1 b2]]
                      (let [f (cond
                               (< (count (hash-set a1 a2 b1 b2)) 3) nil
                               (= a1 b1) [b1 b2 a2]
                               (= a2 b2) [b1 a2 a1]
                               :default [b1 b2 a2 a1])]
                        (if (and f face-fn) (face-fn f) [f])))]
      (->> (d/successive-nth 2 strips)
           (mapcat ;; TODO transduce
            (fn [[sa sb]]
              (mapcat make-face
                   (d/successive-nth 2 sa)
                   (d/successive-nth 2 sb))))
           (tu/into-mesh (gmesh) add-face))))
#+END_SRC

*** Predefined forms

#+BEGIN_SRC clojure :noweb-ref ctors
  (defn saddle
    [s]
    (let [sv (vec3 s)]
      (reduce
       (fn [m [p flags]]
         (tu/into-mesh m add-face (g/as-mesh (thi.ng.geom.types.AABB p s) {:flags flags})))
       (gmesh)
       [[(vec3) :ewsfb]
        [(vec3 0 s 0) :wfb]
        [(vec3 s s 0) :ensfb]
        [(vec3 0 (* s 2) 0) :ewnfb]])))
#+END_SRC

** Protocol implementations

#+BEGIN_SRC clojure :noweb-ref impl
  (extend-type thi.ng.geom.types.GMesh
#+END_SRC

*** PArea
#+BEGIN_SRC clojure :noweb-ref impl
  g/PArea
  (area
   [_]
   (transduce
    (comp
     (mapcat gu/tessellate-with-first)
     (map #(->> % (apply gu/tri-area3) m/abs)))
    + (:faces _)))
#+END_SRC

*** TODO PBoundary

#+BEGIN_SRC clojure :noweb-ref impl

#+END_SRC

*** PBounds

#+BEGIN_SRC clojure :noweb-ref impl
  g/PBounds
  (bounds [_] (tu/bounding-box (keys (:vertices _))))
  (width [_] (gu/axis-range 0 (keys (:vertices _))))
  (height [_] (gu/axis-range 1 (keys (:vertices _))))
  (depth [_] (gu/axis-range 2 (keys (:vertices _))))
#+END_SRC

*** PBoundingSphere

#+BEGIN_SRC clojure :noweb-ref impl
  g/PBoundingSphere
  (bounding-sphere
   [_] (tu/bounding-sphere (g/centroid _) (g/vertices _)))
#+END_SRC

*** TODO PCenter

#+BEGIN_SRC clojure :noweb-ref impl
  g/PCenter
  (center
   ([_] (g/center _ (vec3)))
   ([_ o] (g/transform _ (g/translate M44 (g/- o (g/centroid _))))))
  (centroid
   [_] (gu/centroid (keys (:vertices _))))
#+END_SRC

*** PFlip

#+BEGIN_SRC clojure :noweb-ref impl
  g/PFlip
  (flip [_] (tu/map-mesh (fn [f] [(vec (rseq f))]) _))
#+END_SRC

*** TODO PGraph

#+BEGIN_SRC clojure :noweb-ref impl
  g/PGraph
  (connected-components
   [_] [_]) ;; TODO
  (vertex-neighbors
   [_ v] (vertex-neighbors* _ v))
  (vertex-valence
   [_ v] (vertex-valence* _ v))
  (remove-vertex
   [_ v] (remove-vertex* _ v))
  (replace-vertex
   [_ v v2] (replace-vertex* _ v v2))
  (merge-vertices
   [_ a b] (merge-vertices* _ a b))
#+END_SRC

*** PVertexAccess, PEdgeAccess & PFaceAccess

#+BEGIN_SRC clojure :noweb-ref impl
  g/PVertexAccess
  (vertices
   [_] (keys (:vertices _)))
  g/PEdgeAccess
  (edges
   [_] (keys (:edges _)))
  g/PFaceAccess
  (faces
   [_] (:faces _))
  (add-face
   [_ f] (add-face _ f))
  (vertex-faces
   [_ v] (vertex-faces* _ v))
  (remove-face
   [{:keys [vertices edges faces fnormals vnormals] :as _} f]
   (if (get faces f)
     (loop [verts vertices
            vnorms vnormals
            edges edges
            fedges (d/successive-nth 2 (conj f (first f)))]
       (if fedges
         (let [[a b] (first fedges)
               e #{a b}
               efaces (disj (get edges e) f)
               edges (if (seq efaces)
                       (assoc edges e efaces)
                       (dissoc edges e))
               ve (filter #(not= (:f %) f) (get verts a))]
           (if (seq ve)
             (recur (assoc verts a (into #{} ve)) vnorms edges (next fedges))
             (recur (dissoc verts a) (dissoc vnorms a) edges (next fedges))))
         (assoc _
           :vertices verts
           :vnormals vnorms
           :edges edges
           :faces (disj faces f)
           :fnormals (dissoc fnormals f))))
     _))
#+END_SRC

*** PNormalAccess

#+BEGIN_SRC clojure :noweb-ref impl
  g/PNormalAccess
  (face-normals
   [_ force?]
   (if (seq (:fnormals _))
     (:fnormals _)
     (if force? (:fnormals (g/compute-face-normals _)))))
  (face-normal
   [_ f] ((:fnormals _) f))
  (vertex-normals
   [_ force?]
   (if (seq (:vnormals _))
     (:vnormals _)
     (if force? (:vnormals (g/compute-vertex-normals _)))))
  (vertex-normal
   [_ v] ((:vnormals _) v))
  (compute-face-normals
   [_]
   (loop [norms (transient #{}), fnorms (transient {}), faces (:faces _)]
     (if faces
       (let [f (first faces)
             [norms n] (d/index! norms (apply gu/ortho-normal f))]
         (recur norms (assoc! fnorms f n) (next faces)))
       (assoc _
              :normals  (persistent! norms)
              :fnormals (persistent! fnorms)))))
  (compute-vertex-normals
   [_]
   (let [{:keys [vertices normals fnormals] :as this} (if (seq (:fnormals _)) _ (g/compute-face-normals _))
         ntx (map #(get fnormals %))]
     (loop [norms (transient normals), vnorms (transient (hash-map)), verts (keys vertices)]
       (if verts
         (let [v (first verts)
               [norms n] (->> (d/value-set :f vertices v)
                              (transduce ntx g/+ v/V3)
                              (g/normalize)
                              (d/index! norms))]
           (recur norms (assoc! vnorms v n) (next verts)))
         (assoc this
                :normals  (persistent! norms)
                :vnormals (persistent! vnorms))))))
#+END_SRC

*** PGeomContainer

#+BEGIN_SRC clojure :noweb-ref impl
  g/PGeomContainer
  (into
   [_ faces] (tu/into-mesh _ add-face faces))
  g/PClear
  (clear*
   [_] (gmesh))
#+END_SRC

*** PMeshConvert

#+BEGIN_SRC clojure :noweb-ref impl
  g/PMeshConvert
  (as-mesh
   ([_] _)
   ([_ opts] (g/into (:mesh opts) (:faces _))))
#+END_SRC

*** PTessellate

#+BEGIN_SRC clojure :noweb-ref impl
  g/PTessellate
  (tessellate
   ([_]      (g/tessellate _ {}))
   ([_ opts] (tu/map-mesh (or (:fn opts) gu/tessellate-with-first) _)))
#+END_SRC

*** PTransform

#+BEGIN_SRC clojure :noweb-ref impl
  g/PScale
  (scale
   ([_ s]
      (tu/transform-mesh _ add-face #(g/* % s)))
   ([_ sx sy sz]
      (tu/transform-mesh _ add-face #(g/* % sx sy sz))))
  (scale-size
   [_ s]
   (let [c (g/centroid _)]
     (tu/transform-mesh _ add-face #(g/madd (g/- % c) s c))))
  g/PTranslate
  (translate
   [_ t] (tu/transform-mesh _ add-face #(g/+ % t)))
  g/PTransform
  (transform
   [_ tx]
   (tu/transform-mesh _ add-face tx))
#+END_SRC

*** PVolume

#+BEGIN_SRC clojure :noweb-ref impl
  g/PVolume
  (volume
   [_] (gu/total-volume (:faces _)))
#+END_SRC

*** End of implementation                                          :noexport:

#+BEGIN_SRC clojure :noweb-ref impl
  )
#+END_SRC

** Complete namespace definition

#+BEGIN_SRC clojure :tangle ../babel/src/thi/ng/geom/gmesh.cljc :noweb yes :mkdirp yes :padline no
  (ns thi.ng.geom.gmesh
    (:require
     [thi.ng.geom.core :as g]
     [thi.ng.geom.core.utils :as gu]
     [thi.ng.geom.core.vector :as v :refer [vec2 vec3]]
     [thi.ng.geom.core.matrix :refer [M44]]
     [thi.ng.geom.types]
     [thi.ng.geom.types.utils :as tu]
     [thi.ng.dstruct.core :as d]
     [thi.ng.math.core :as m :refer [*eps*]]
     [clojure.core.reducers :as r]
     [clojure.set :as set]))

  <<helpers>>

  <<ctors>>

  <<tessellate>>

  <<impl>>
#+END_SRC
