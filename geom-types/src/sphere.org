#+SETUPFILE: ../../src/setup.org

* Contents                                                         :toc_4_gh:
 - [[#thinggeomsphere][thi.ng.geom.sphere]]
     - [[#constructor][Constructor]]
     - [[#type-specific-functions][Type specific functions]]
     - [[#protocol-implementations][Protocol implementations]]
         - [[#parea][PArea]]
         - [[#pboundary][PBoundary]]
         - [[#pbounds][PBounds]]
         - [[#pboundingsphere][PBoundingSphere]]
         - [[#pcenter][PCenter]]
         - [[#pclassify][PClassify]]
         - [[#todo-pgraph][TODO PGraph]]
         - [[#pintersect][PIntersect]]
         - [[#todo-pmeshconvert][TODO PMeshConvert]]
         - [[#pproximity][PProximity]]
         - [[#psample][PSample]]
         - [[#ptessellate][PTessellate]]
         - [[#todo-transformations][TODO Transformations]]
             - [[#todo-scale-with-non-uniform-values-should-return-ellipsoid][TODO scale with non-uniform values should return Ellipsoid]]
         - [[#pvolume][PVolume]]
         - [[#end-of-implementation][End of implementation]]
     - [[#namespace-declaration][Namespace declaration]]

* thi.ng.geom.sphere

** Constructor

#+BEGIN_SRC clojure :noweb-ref ctor
  (defn sphere
    ([] (thi.ng.geom.types.Sphere. (vec3) 1.0))
    ([r] (thi.ng.geom.types.Sphere. (vec3) #?(:clj (double r) :cljs r)))
    ([p r] (thi.ng.geom.types.Sphere. (vec3 p) #?(:clj (double r) :cljs r))))
#+END_SRC

** Type specific functions

#+BEGIN_SRC clojure :noweb-ref helpers

#+END_SRC

** Protocol implementations

#+BEGIN_SRC clojure :noweb-ref impl
  (extend-type thi.ng.geom.types.Sphere
#+END_SRC

*** PArea

#+BEGIN_SRC clojure :noweb-ref impl
  g/PArea
  (area
   [{r :r}] (* 4.0 PI r r))
#+END_SRC

*** PBoundary

#+BEGIN_SRC clojure :noweb-ref impl
  g/PBoundary
  (contains-point?
   [{p :p r :r} q] (<= (g/dist-squared p q) (* r r)))
#+END_SRC

*** PBounds

#+BEGIN_SRC clojure :noweb-ref impl
  g/PBounds
  (bounds
   [_] (thi.ng.geom.types.AABB. (g/- (:p _) (:r _)) (vec3 (* 2 (:r _)))))
  (width  [_] (* 2.0 (:r _)))
  (height [_] (* 2.0 (:r _)))
  (depth  [_] (* 2.0 (:r _)))
#+END_SRC

*** PBoundingSphere

#+BEGIN_SRC clojure :noweb-ref impl
  g/PBoundingSphere
  (bounding-sphere [_] _)
#+END_SRC

*** PCenter

#+BEGIN_SRC clojure :noweb-ref impl
  g/PCenter
  (center
   ([_] (thi.ng.geom.types.Sphere. (vec3) (:r _)))
   ([_ p] (thi.ng.geom.types.Sphere. (vec3 p) (:r _))))
  (centroid [_] (:p _))
#+END_SRC

*** PClassify

#+BEGIN_SRC clojure :noweb-ref impl
  g/PClassify
  (classify-point
   [{p :p r :r} q]
   (m/signum (- (* r r) (g/dist-squared p q)) *eps*))
#+END_SRC

*** TODO PGraph

#+BEGIN_SRC clojure :noweb-ref impl

#+END_SRC

*** PIntersect

If ray intersects sphere, the function will return a 2-element vector
of distances from the ray's start point to the sphere surface. If
either of those distances is negative the intersection point lies in
the opposite ray direction. The actual intersection points can be
computed by passing the distances into the following form:

#+BEGIN_SRC clojure
  ;; dir = ray direction
  ;; p = ray start point
  ;; t = distance
  (g/madd dir t p)
#+END_SRC

If there's no intersection, the function returns =nil=.

#+BEGIN_SRC clojure :noweb-ref impl
  g/PIntersect
  (intersect-ray
   [{p :p r :r} ray]
   (let [[rp dir] (if (map? ray) [(:p ray) (:dir ray)] ray)]
     (isec/intersect-ray-sphere? rp dir p r)))
  (intersect-shape
   [_ s]
   (cond
    (instance? thi.ng.geom.types.AABB s)
    (isec/intersect-aabb-sphere? s _)
    (instance? thi.ng.geom.types.Sphere s)
    (isec/intersect-sphere-sphere? _ s)
    (instance? thi.ng.geom.types.Plane s)
    (isec/intersect-plane-sphere? (:n s) (:w s) (:p _) (:r _))
    :default (err/type-error! "Sphere" s)))
#+END_SRC

*** TODO PMeshConvert

#+BEGIN_SRC clojure :noweb-ref impl
  g/PMeshConvert
  (as-mesh
   ([_] (g/as-mesh _ {}))
   ([{[x y z] :p r :r} {:keys [mesh res slices stacks] :or {res *resolution*}}]
      (let [slices (or slices res), stacks (or stacks res)
            range-u (range slices), range-v (range stacks)]
        (->> (for [i range-u, j range-v
                   :let [u (/ i slices)
                         v (/ j stacks)
                         u1 (/ (inc i) slices)
                         v1 (/ (inc j) stacks)
                         verts [[u v]]
                         verts (if (pos? j) (conj verts [u1 v]) verts)
                         verts (if (< j (dec stacks)) (conj verts [u1 v1]) verts)]]
               (conj verts [u v1]))
             ;; TODO transduce
             (map
              (fn [verts]
                (map
                 (fn [[u v]]
                   (let [theta (* TWO_PI u) ;; FIXME optimize trig
                         phi (* PI v)
                         st (Math/sin theta) ct (Math/cos theta)
                         sp (Math/sin phi) cp (Math/cos phi)]
                     (vec3
                      (+ (mm/mul ct sp r) x)
                      (mm/madd cp r y)
                      (+ (mm/mul st sp r) z))))
                 verts)))
             (g/into (or mesh (bm/basic-mesh)))))))
#+END_SRC

*** PProximity

#+BEGIN_SRC clojure :noweb-ref impl
  g/PProximity
  (closest-point
   [{p :p r :r} q]
   (g/+! (g/normalize (g/- q p) r) p))
#+END_SRC

*** PSample

#+BEGIN_SRC clojure :noweb-ref impl
  g/PSample
  (random-point-inside
   [_]
   (g/+ (:p _) (v/randvec3 (m/random (:r _)))))
  (random-point
   [_]
   (g/+ (:p _) (v/randvec3 (:r _))))
#+END_SRC

*** PTessellate

#+BEGIN_SRC clojure :noweb-ref impl
  g/PTessellate
  (tessellate
   [_] (g/tessellate _ {}))
  (tessellate
   [_ opts] (g/tessellate (g/as-mesh _ opts)))
#+END_SRC

*** TODO Transformations
**** TODO scale with non-uniform values should return Ellipsoid

#+BEGIN_SRC clojure :noweb-ref impl
  g/PRotate
  (rotate
   [_ theta] (thi.ng.geom.types.Sphere. (g/rotate-z (:p _) theta) (:r _)))
  g/PRotate3D
  (rotate-x
   [_ theta] (thi.ng.geom.types.Sphere. (g/rotate-x (:p _) theta) (:r _)))
  (rotate-y
   [_ theta] (thi.ng.geom.types.Sphere. (g/rotate-y (:p _) theta) (:r _)))
  (rotate-z
   [_ theta] (thi.ng.geom.types.Sphere. (g/rotate-z (:p _) theta) (:r _)))
  (rotate-around-axis
   [_ axis theta]
   (thi.ng.geom.types.Sphere.
    (g/rotate-around-axis (:p _) axis theta) (:r _)))
  
  g/PScale
  (scale [_ s] (thi.ng.geom.types.Sphere. (g/* (:p _) s) (* (:r _) s)))
  (scale-size [_ s] (thi.ng.geom.types.Sphere. (:p _) (* (:r _) s)))

  g/PTranslate
  (translate [_ t] (thi.ng.geom.types.Sphere. (g/+ (:p _) t) (:r _)))
#+END_SRC

*** PVolume

#+BEGIN_SRC clojure :noweb-ref impl
  g/PVolume
  (volume [{r :r}] (mm/mul (/ 4.0 3.0) PI r r r))
#+END_SRC

*** End of implementation                                          :noexport:

#+BEGIN_SRC clojure :noweb-ref impl
  )
#+END_SRC

** Namespace declaration

#+BEGIN_SRC clojure :tangle ../babel/src/thi/ng/geom/sphere.cljc :noweb yes :mkdirp yes :padline no
  (ns thi.ng.geom.sphere
    #?(:cljs (:require-macros [thi.ng.math.macros :as mm]))
    (:require
     [thi.ng.geom.core :as g :refer [*resolution*]]
     [thi.ng.geom.core.utils :as gu]
     [thi.ng.geom.core.vector :as v :refer [vec3]]
     [thi.ng.geom.core.intersect :as isec]
     [thi.ng.geom.basicmesh :as bm]
     [thi.ng.geom.types :as types]
     [thi.ng.xerror.core :as err]
     [thi.ng.math.core :as m :refer [TWO_PI PI *eps*]]
     #?(:clj [thi.ng.math.macros :as mm]))
    #?(:clj
       (:import
        [thi.ng.geom.types AABB Sphere])))

  <<ctor>>

  <<helpers>>

  <<impl>>
#+END_SRC
